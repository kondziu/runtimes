use std::str::FromStr;
use std::collections::VecDeque;
use fml_ast::{AST, Operator,Identifier};

grammar;

// Lexer
match {
    r"\s*" => { }, // ignore whitespace
    r"\(\*([^\*]*\*+[^\*\(])*([^\*]*\*+|[^\*])*\*\)" => { }, // ignore

    // all keywords and fixed tokens
    ";" => SEMICOLON,
    "begin" => BEGIN,
    "end" => END,
    "|" => OR,
    "&" => AND,
    "==" => EQUAL,
    "!=" => UNEQUAL,
    ">" => GREATER,
    "<" => LESS,
    ">=" => GREATER_EQUAL,
    "<=" => LESS_EQUAL,
    "+" => PLUS,
    "-" => MINUS,
    "/" => DIVIDE,
    "*" => MULTIPLY,
    "%" => MODULE,
    "if" => IF,
    "then" => THEN,
    "else" => ELSE,
    "(" => LPAREN,
    ")" => RPAREN,
    "let" => LET,
    "=" => BE,
    "null" => UNIT,
    "<-" => LARROW,
    "->" => RARROW,
    "print" => PRINT,
    "object" => OBJECT,
    "extends" => EXTENDS,
    "." => DOT,
    "[" => LBRACKET,
    "]" => RBRACKET,
    "while" => WHILE,
    "do" => DO,
    "function" => FUNCTION,
    "array" => ARRAY,
    "," => COMMA,
    "true" => TRUE,
    "false" => FALSE,
    "this" => THIS,

    // all fungible tokens
    r"[_A-Za-z][_A-Za-z0-9]*" => IDENTIFIER,
    r"[-]?[0-9]+" => NUMBER,
    r#""([^\\]|\\[~ntr\\])*""# => STRING_LITERAL,
}

// Parser

pub TopLevel: AST = {
    <expressions: Expressions> => expressions,
                               => AST::Unit,
}

Expressions: AST = {
    <element: Expression<"open">> <elements: (SEMICOLON <Expression<"open">>)*> SEMICOLON? => {
        if elements.is_empty() {
            element
        } else {
            let mut all = VecDeque::from(elements);
            all.push_front(element);
            AST::Block(put_into_boxes!(Vec::from(all)))
        }
    }
}

Expression<openness>: AST = {
    FunctionDefinition<openness>      => <>, // Feeny-style function definition, returns unit?
    ObjectDefinition                  => <>, // prototype object definition
    Assignment<openness>              => <>, // Feenified variable definition
    //Field                             => <>, // field access
    Conditional<openness>             => <>, // conditional expression
    Loop<openness>                    => <>, // loop expression
    Mutation<openness>                => <>, // variable/object member mutation
    Print                             => <>, // call to the print built-in
    Operation                         => <>, // operations, but this also leads to Accessible
}


Block: AST = {
    BEGIN END                            => AST::Unit,
    BEGIN <expressions: Expressions> END => expressions
}

Accessible: AST = {
    ParenthesizedExpression           => <>, // expression in parentheses
    Block                             => <>, // expression block
    Application                       => <>, // function application
    ArrayDefinition                   => <>, // array definition
    ArrayAccess                       => <>, // array access
    VariableAccess                    => <>, // variable access
    Literal                           => <>, // literal, duh
}

Operation: AST = {
    Disjunction                       => <>, // Let's climb the operator precedence!
}

Disjunction: AST = {
    <head: Conjunction> <tail: (<DisjunctionOperator> <Conjunction>)*> =>
        make_operator_ast!(head, tail)
}

Conjunction: AST = {
    <head: Comparison> <tail: (<ConjunctionOperator> <Comparison>)*> =>
        make_operator_ast!(head, tail)
}

Comparison: AST = {
    <head: Additive> <tail: (<EqualityOperator> <Additive>)*> =>
        make_operator_ast!(head, tail)
}

Additive: AST = {
    <head: Factor> <tail: (<AdditiveOperator> <Factor>)*> =>
        make_operator_ast!(head, tail)
}

Operand: AST = {
    Accessible => <>,
    <object: Accessible> DOT <fields: (<Ident> DOT)*> <field: Ident> => {
        let mut tail = Vec::from(fields);
        tail.push(field);
        tail.into_iter().fold(object, |left, right| AST::FieldAccess {object: Box::new(left), field: right})
    },
}

Factor: AST = {
    <head: Operand> <tail: (<FactorOperator> <Operand>)*> => make_operator_ast!(head, tail),
}

Operator: Operator = {
    ConjunctionOperator => <>,
    DisjunctionOperator => <>,
    EqualityOperator    => <>,
    AdditiveOperator    => <>,
    FactorOperator      => <>,
}

ConjunctionOperator: Operator = {
    AND => Operator::Conjunction,
}

DisjunctionOperator: Operator = {
    OR => Operator::Disjunction,
}

EqualityOperator: Operator = {
    EQUAL => Operator::Equality,
    UNEQUAL => Operator::Inequality,
    GREATER  => Operator::Greater,
    GREATER_EQUAL => Operator::GreaterEqual,
    LESS => Operator::Less,
    LESS_EQUAL => Operator::LessEqual,
}

AdditiveOperator: Operator = {
    PLUS => Operator::Addition,
    MINUS => Operator::Subtraction,
}

FactorOperator: Operator = {
    MULTIPLY => Operator::Multiplication,
    DIVIDE => Operator::Division,
    MODULE => Operator::Module,
}

Literal: AST = {
    Number                  => <>,
    Boolean                 => <>,
    Unit                    => <>,
}

ParenthesizedExpression: AST = {
    LPAREN <Expression<"open">> RPAREN    => <>
}

Member: AST = {
    Assignment<"open">         => <>,
    FunctionDefinition<"open"> => <>,
    OperatorDefinition<"open"> => <>,
}

Conditional<openness>: AST = {
    IF <condition: Expression<"open">> THEN <consequent: Expression<"closed">>
                                       ELSE <alternative: Expression<openness>> =>
        AST::Conditional{condition: Box::new(condition),
                         consequent: Box::new(consequent),
                         alternative: Box::new(alternative)},
    IF <condition: Expression<"open">> THEN <consequent: Expression<"open">> if openness == "open" =>
        AST::Conditional{condition: Box::new(condition),
                         consequent: Box::new(consequent),
                         alternative: Box::new(AST::Unit)},
}

Print: AST = { // TODO format string check
    PRINT LPAREN <string: String> <arguments: (COMMA <Arguments>)?> RPAREN => {
        let boxed_arguments: Vec<Box<AST>> = match arguments {
            Some(arguments) => arguments.into_iter().map( | e | Box::new(e)).collect(),
            None => vec!(),
        };
        AST::Print {format: string, arguments: boxed_arguments}
    }
}

ObjectDefinition: AST = {
    OBJECT <extends: (EXTENDS <Expression<"open">>)?> <members: Members> =>
        AST::ObjectDefinition {extends: option_into_box!(extends),
                               members: put_into_boxes!(members)}
}

Members: Vec<AST> = {
    BEGIN <elements: (<Member> SEMICOLON)*> <element: Member?> END => match element {
        None => elements,
        Some(e) => { let mut elements = elements; elements.push(e); elements }
    }
}

Field: AST = {
    <object: Accessible> DOT <objects: (<Ident> DOT)*> <field: Ident> => {
        let mut tail = Vec::from(objects);
        tail.push(field);
        tail.into_iter().fold(object, |left, right|
            AST::FieldAccess {object: Box::new(left), field: right})
    }
}

ArrayDefinition: AST = {
    ARRAY LPAREN <size: Expression<"open">> COMMA <value: Expression<"open">> RPAREN =>
        AST::ArrayDefinition {size: Box::new(size), value: Box::new(value)}
}

ArrayAccess: AST = {
    <array: Accessible> LBRACKET <index: Expression<"open">> RBRACKET =>
        AST::ArrayAccess {array: Box::new(array), index: Box::new(index)},
    <array: Field> LBRACKET <index: Expression<"open">> RBRACKET =>
        AST::ArrayAccess {array: Box::new(array), index: Box::new(index)}
}

Loop<openness>: AST = {
    WHILE <condition: Expression<"open">> DO <body: Expression<openness>> =>
        AST::Loop{condition: Box::new(condition), body: Box::new(body)}
}

FunctionDefinition<openness>: AST = {
    FUNCTION <id: Ident> <parameters: Parameters> LARROW <body: Expression<openness>> =>
        AST::FunctionDefinition{function: id,
                                parameters: parameters,
                                body: Box::new(body)}
}

OperatorDefinition<openness>: AST = {
    FUNCTION <operator: Operator> <parameters: Parameters> LARROW <body: Expression<openness>> =>
        AST::OperatorDefinition{operator: operator,
                                parameters: parameters,
                                body: Box::new(body)}
}

Parameters: Vec<Identifier> = {
    LPAREN <elements: (<Ident> COMMA)*> <element: Ident?> RPAREN =>
        match element {
            None => elements,
            Some(e) => { let mut elements = elements; elements.push(e); elements }
        }
}

Application: AST = {
    <function: Ident> LPAREN <arguments: Arguments> RPAREN => {
        let boxed_arguments: Vec<Box<AST>> =
            arguments.into_iter().map(|e| Box::new(e)).collect();
        AST::FunctionCall{function: function, arguments: boxed_arguments}
    },
    <object: Accessible> DOT <fields: (<Ident> DOT)*> <method: Ident> LPAREN <arguments: Arguments> RPAREN => {
        let tail = Vec::from(fields);
        let host:AST = tail.into_iter().fold(object, |left, right| AST::FieldAccess {object: Box::new(left), field: right});
        let boxed_arguments: Vec<Box<AST>> = arguments.into_iter().map(|e| Box::new(e)).collect();
        AST::MethodCall{object: Box::new(host), method, arguments: boxed_arguments}
    },
    <object: Accessible> DOT <fields: (<Ident> DOT)*> <operator: Operator> LPAREN <arguments: Arguments> RPAREN => {
        let tail = Vec::from(fields);
        let host:AST = tail.into_iter().fold(object, |left, right| AST::FieldAccess {object: Box::new(left), field: right});
        let boxed_arguments: Vec<Box<AST>> = arguments.into_iter().map(|e| Box::new(e)).collect();
        AST::OperatorCall{object: Box::new(host), operator, arguments: boxed_arguments}
    }
}

Arguments: Vec<AST> = {
    <elements: (<Expression<"open">> COMMA)*> <element: Expression<"open">?> => match element {
        None => elements,
        Some(e) => { let mut elements = elements; elements.push(e); elements }
    }
}

Assignment<openness>: AST = {
    LET <id: Ident> BE <v: Expression<openness>> =>
        AST::VariableDefinition{name: id, value: Box::new(v)},
}

Mutation<openness>: AST = {
    <id: Ident> LARROW <v: Expression<openness>> =>
        AST::VariableMutation{name: id, value: Box::new(v)},
    <object: Accessible> DOT <fields: (<Ident> DOT)*> <field: Ident> LARROW <v: Expression<openness>> => {
        let tail = Vec::from(fields);
        let host: AST = tail.into_iter().fold(object, |left, right| AST::FieldAccess {object: Box::new(left), field: right});
        AST::FieldMutation {object: Box::new(host), field: field, value: Box::new(v)}
    },
    <array: Accessible> LBRACKET <index: Expression<"open">> RBRACKET LARROW <v: Expression<openness>> =>
        AST::ArrayMutation{array: Box::new(array), index: Box::new(index), value: Box::new(v)},
    <array: Field> LBRACKET <index: Expression<"open">> RBRACKET LARROW <v: Expression<openness>> =>
        AST::ArrayMutation {array: Box::new(array), index: Box::new(index), value: Box::new(v)}
}

VariableAccess: AST = {
    Ident                               => AST::VariableAccess { name: <> },
}

Ident: Identifier = {
    IDENTIFIER                          => Identifier::from(<>),
    THIS                                => Identifier::from(<>),
}

Number: AST = {
    NUMBER                              => AST::Number(i32::from_str(<>).unwrap()),
}

String: String = {
    <s:STRING_LITERAL>                   => s[1..s.len()-1].to_string(),
}

Boolean: AST = {
    TRUE                                 => AST::Boolean(true),
    FALSE                                => AST::Boolean(false),
}

Unit: AST = {
    UNIT                                 => AST::Unit,
}
